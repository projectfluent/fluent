
/* An FTL file defines a Resource. */
Resource            ::= (Entry | blank_block | junk_line)*
Entry               ::= Message
                      | Term
                      | CommentLine
Message             ::= Identifier blank_inline? "=" ((Pattern Attribute*) | (Attribute+))
Term                ::= TermIdentifier blank_inline? "=" Value Attribute*
CommentLine         ::= ("###" | "##" | "#") ("\u0020" /.*/)? line_end

/* Adjacent junk_lines should be joined into FTL.Junk during the AST
   construction. */
junk_line           ::= /.*/ line_end

/* Attributes of Messages and Terms. */
Attribute           ::= blank? "." Identifier blank_inline? "=" Pattern

/* Value types: Pattern and VariantList. */
Value               ::= VariantList
                      | Pattern
VariantList         ::= blank? "{" variant_list blank? "}"
Pattern             ::= (PatternInline PatternBlock)
                      | PatternInline
                      | PatternBlock
PatternInline       ::= blank_inline? PatternElement+ line_end
PatternBlock        ::= blank_block* pattern_line (pattern_line | blank_block)*
pattern_line        ::= PatternStartElement PatternElement* line_end
PatternElement      ::= TextElement
                      | Placeable
PatternStartElement ::= (blank_inline text_start)
                      | (blank_inline? Placeable)
TextElement         ::= text_char+
Placeable           ::= "{" blank? (SelectExpression | InlineExpression) blank? "}"

/* Rules for validating expressions in Placeables and as selectors of
 * SelectExpressions are documented in spec/valid.md and enforced in
 * syntax/abstract.mjs. */
InlineExpression    ::= StringLiteral
                      | NumberLiteral
                      | VariableReference
                      | CallExpression
                      | AttributeExpression
                      | VariantExpression
                      | MessageReference
                      | TermReference
                      | Placeable

/* Literals */
StringLiteral       ::= quote quoted_text_char* quote
NumberLiteral       ::= "-"? digit+ ("." digit+)?

/* Inline Expressions */
MessageReference    ::= Identifier
TermReference       ::= TermIdentifier
VariableReference   ::= VariableIdentifier
CallExpression      ::= Function blank? "(" blank? argument_list blank? ")"
argument_list       ::= (Argument blank? "," blank?)* Argument?
Argument            ::= NamedArgument
                      | InlineExpression
NamedArgument       ::= Identifier blank? ":" blank? (StringLiteral | NumberLiteral)
AttributeExpression ::= (MessageReference | TermReference) "." Identifier
VariantExpression   ::= TermReference VariantKey

/* Block Expressions */
SelectExpression    ::= InlineExpression blank? "->" variant_list
variant_list        ::= blank_block Variant* DefaultVariant Variant*
Variant             ::= blank? VariantKey Value
DefaultVariant      ::= blank? "*" VariantKey Value
VariantKey          ::= "[" blank? (NumberLiteral | VariantName) blank? "]"
VariantName         ::= word (blank word)*

/* Identifiers */
Identifier          ::= identifier
TermIdentifier      ::= "-" identifier
VariableIdentifier  ::= "$" identifier
Function            ::= [A-Z] [A-Z_?-]*

/* Tokens */
identifier          ::= [a-zA-Z] [a-zA-Z0-9_-]*
word                ::= (regular_char - backslash - "}" - "{" - "]" - "[" - "=")+

/* Characters */
backslash           ::= "\\"
quote               ::= "\""
/* Any Unicode character from BMP excluding C0 control characters, space,
 * surrogate blocks and non-characters (U+FFFE, U+FFFF).
 * Cf. https://www.w3.org/TR/REC-xml/#NT-Char
 * TODO Add characters from other planes: U+10000 to U+10FFFF.
 */
regular_char        ::= [!-\uD7FF\uE000-\uFFFD]
text_char           ::= blank_inline
                      | /\\u[0-9a-fA-F]{4}/
                      | (backslash backslash)
                      | (backslash "{")
                      | (regular_char - "{" - backslash)
text_start          ::= text_char - "}" - "[" - "*" - "."
quoted_text_char    ::= (text_char - quote)
                      | (backslash quote)
digit               ::= [0-9]

/* Whitespace */
blank_inline        ::= ("\u0020" | "\u0009")+
line_end            ::= "\u000D\u000A"
                      | "\u000A"
                      | EOF
blank_block         ::= (blank_inline? line_end)+
blank               ::= (blank_inline | line_end)+
